plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.11.0'
}

sourceCompatibility = 1.8

repositories {
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

//allow publish key/secret override from environment variable
task setupPluginUpload {
    doLast {
        String key = System.env.gradlePublishKey
        String secret = System.env.gradlePublishSecret
        if(!key || !secret) {
            throw new RuntimeException("gradlePublishKey and/or gradlePublishSecret are not defined environment variables")
        }

        System.properties.setProperty('gradle.publish.key', System.env.gradlePublishKey)
        System.properties.setProperty('gradle.publish.secret', System.env.gradlePublishSecret)
    }
    tasks.publishPlugins.dependsOn(delegate)
}

version = '0.1.0'
group 'com.iptech.gradle'
description = 'A plugin that will execute scripts and build Unity projects'

pluginBundle {
    website = 'https://github.com/ianpilipski/IPTech-GradleUnityPlugin'
    vcsUrl = 'https://github.com/ianpilipski/IPTech-GradleUnityPlugin'
    tags = ['unity', 'unity3d', 'iptech']
}

gradlePlugin {
    plugins {
        unityPlugin {
            id = 'com.iptech.gradle.unity-plugin'
            displayName = 'IPTech Unity Plugin'
            description = 'A plugin that will execute scripts and build Unity projects'
            implementationClass = 'com.iptech.gradle.unity.UnityPlugin'
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation 'gradle.plugin.com.iptech.gradle:iptech-nunit-plugin:0.1.0'
    implementation 'gradle.plugin.com.iptech.gradle:iptech-xcode-plugin:0.1.0'
}
